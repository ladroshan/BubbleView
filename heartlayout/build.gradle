apply plugin: 'com.android.library'
//应用插件
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//version = "1.2.7"

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

/*publish {
    userOrg = 'superman'
    groupId = 'com.wutianlong'     //jcenter上的路径
    artifactId = 'bubbleviews'     //项目名称
    publishVersion = '1.0.0'       //版本号
    desc = 'Oh hi, this is a nice bubbleview  for a project'   //描述，不重要
    website = 'https://github.com/wutianlong/BubbleView'   //网站，不重要；尽量模拟github上的地址，例如我这样的；当然你有地址最好了
}*/




def siteUrl = 'https://github.com/wutianlong/BubbleView'      // 项目的主页
def gitUrl = 'https://github.com/wutianlong/BubbleView.git'   // Git仓库的url
group = "com.wutianlong"                   // Maven Group ID for the artifact，一般填你唯一的包名

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Oh hi, this is a nice bubbleview  for a project' 	//项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'wutianlong'		//填写的一些基本信息
                        name 'wutianlong'
                        email 'wutianlong0701@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "bubbleviews"	//发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true

        version {
            vcsTag = '1.2.7'
        }
    }
}
